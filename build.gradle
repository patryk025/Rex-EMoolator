buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:8.12.3'
        classpath 'com.gradleup.shadow:shadow-gradle-plugin:9.2.2'
    }
}

allprojects {
    apply plugin: "eclipse"

    version = System.getenv('RELEASE_VERSION') ?: '0.1-SNAPSHOT'
    ext {
        appName = "Rex EMoolator"
        gdxVersion = '1.14.0'
        roboVMVersion = '2.3.20'
        ashleyVersion = '1.7.4'
        aiVersion = '1.8.2'
        gdxControllersVersion = '2.2.4'
        antlrVersion = '4.13.2'
        ini4jVersion = '0.5.4'
        oshiVersion = '6.6.3'
        jnaVersion = '5.18.1'
        ode4jVersion = '0.5.4'
    }

    repositories {
        mavenLocal()
        mavenCentral()
        google()
        gradlePluginPortal()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
        maven { url "https://jitpack.io" }
    }
}

project(":desktop") {
    apply plugin: "java-library"
    apply plugin: "com.gradleup.shadow"

    dependencies {
        implementation project(":core")
        api "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
        api "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        api "com.badlogicgames.gdx-controllers:gdx-controllers-desktop:$gdxControllersVersion"
        api "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
        api "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
        implementation "net.java.dev.jna:jna:$jnaVersion"
    }

    shadowJar {
        archiveBaseName.set('Rex_EMoolator-desktop')
        archiveClassifier.set('')
        manifest {
            attributes(
                    'Main-Class': 'pl.genschu.bloomooemulator.GameListFrame'
            )
        }
    }

    tasks.named('jar') {
        enabled = false
    }
    tasks.named('build') {
        dependsOn shadowJar
    }
}

project(":android") {
    apply plugin: "com.android.application"

    configurations { natives }

    dependencies {
        implementation project(":core")
        api "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-arm64-v8a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86_64"
        api "com.badlogicgames.gdx-controllers:gdx-controllers-android:$gdxControllersVersion"
        api "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-arm64-v8a"
        natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-x86"
        natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-x86_64"

        implementation 'androidx.appcompat:appcompat:1.7.1'
        implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
        implementation 'androidx.navigation:navigation-fragment:2.9.6'
        implementation 'androidx.navigation:navigation-ui:2.9.4'
        implementation 'androidx.activity:activity:1.11.0'
        testImplementation 'junit:junit:4.13.2'
        androidTestImplementation 'androidx.test.ext:junit:1.3.0'
        androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
        implementation 'androidx.recyclerview:recyclerview:1.4.0'
        implementation 'androidx.work:work-runtime:2.11.0'

        implementation "net.java.dev.jna:jna:$jnaVersion@aar"
    }
    
    android {
        packagingOptions {
            exclude 'META-INF/AL2.0'
            exclude 'META-INF/LGPL2.1'
        }

        compileOptions {
            sourceCompatibility JavaVersion.VERSION_11
            targetCompatibility JavaVersion.VERSION_11
        }

        applicationVariants.all { variant ->
            variant.outputs.all {
                outputFileName = "Rex_Emoolator-android-${version}-${variant.name}.apk"
            }
        }
    }
}

project(":core") {
    apply plugin: "java-library"
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    dependencies {
        api "com.badlogicgames.gdx:gdx:$gdxVersion"
        api "com.badlogicgames.gdx-controllers:gdx-controllers-core:$gdxControllersVersion"
        api "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
        api "org.antlr:antlr4-runtime:$antlrVersion"
        implementation "org.ini4j:ini4j:$ini4jVersion"
        implementation("com.github.oshi:oshi-core:$oshiVersion") {
            exclude group: 'net.java.dev.jna', module: 'jna'
        }
        implementation "org.ode4j:core:$ode4jVersion"

        testImplementation 'org.junit.jupiter:junit-jupiter:5.13.4'
        testRuntimeOnly "org.junit.platform:junit-platform-launcher:1.13.4"
        testImplementation 'org.mockito:mockito-core:5.20.0'
        testImplementation "org.mockito:mockito-inline:5.2.0"
        testImplementation "org.mockito:mockito-junit-jupiter:5.20.0"
        testImplementation 'org.assertj:assertj-core:3.27.6'
        testImplementation "com.badlogicgames.gdx:gdx-backend-headless:$gdxVersion"
        testRuntimeOnly "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"

        testImplementation("org.openjdk.jmh:jmh-core:1.37")
        testAnnotationProcessor("org.openjdk.jmh:jmh-generator-annprocess:1.37")
    }

    test {
        useJUnitPlatform()
        testLogging {
            events 'passed', 'skipped', 'failed'
        }
        jvmArgs "-javaagent:${configurations.testRuntimeClasspath.find { it.name.contains('mockito-core') }}", '-Xshare:off'
    }

    tasks.register('jmh', JavaExec) {
        group = 'verification'
        description = 'Run JMH benchmarks'

        classpath = sourceSets.test.runtimeClasspath
        mainClass = 'org.openjdk.jmh.Main'

        def resultsDir = layout.buildDirectory.dir("reports/jmh")
        def resultsFile = resultsDir.map { it.file("results.json") }

        doFirst {
            resultsDir.get().asFile.mkdirs()
        }

        args '-rf', 'json'
        args '-rff', resultsFile.get().asFile.absolutePath

        if (project.hasProperty('jmhIncludeRegex')) {
            args project.property('jmhIncludeRegex')
        }
    }
}
